#!/bin/zsh

function ()
{
  # Determine which prompt style to use
  # based on terminal name
  fancy=
  case "$TERM" in
    konsole) fancy=1;;
  esac

  if [ -n "$fancy" ]
  then
    right=$(echo '\uE0B0')        # Right facing arrow
    left=$(echo '\uE0B2')         # Left facing arrow
    bar=$(echo '\u2502')          # Vertical bar
    fore="#DFDFD4"                # Foreground color
    middle="#888888"              # Middleground color
    back="#404454"                # Background color

    PS1=
    PS1+="%K{$back}%F{$fore}"     # colors
    PS1+="%B%n%b@%B%m%b"          # Show user@hostname
    PS1+="%f%k"                   # End colors
    PS1+="%F{$back}$right%f "     # Colored right arrow

    RPS1=
    RPS1+="%F{$back}$left%f"      # Colored left arrow
    RPS1+="%K{$back}%F{$fore}"    # Set colors
    RPS1+="%(?.."                 # Start conditional (nonzero exit code)
    RPS1+="%B%F{red}%?%f%b"       #   Show exit code in red
    RPS1+=" %F{$middle}$bar%f "   #   Vertical bar spacer
    RPS1+=")"                     # End conditional
    RPS1+="%B%F{blue}%~ %f%b"     # Show current directory
    RPS1+="%f%k"                  # End colors
  else
    PS1=
    PS1+="%B%n%b@%B%m%b"          # Show user@hostname
    PS1+="> "                     # Right arrow

    RPS1=
    RPS1+="%(?.."                 # Start conditional (nonzero exit code)
    RPS1+="%B%F{red}%?%f%b"       #   Show exit code in red
    RPS1+=" | "                   #   Vertical bar spacer
    RPS1+=")"                     # End conditional
    RPS1+="%B%F{blue}%~%f%b"      # Show current directory
  fi
}

